macro {

    fun toInt(value: String): Int {
        return value.toInt()
    }

}

tokens {

    companion {
        skip    : [WHITESPACE] ;
    }

    WHITESPACE  : r"[ \r\n\t]" ;

    LPAREN      : "(" ;
    RPAREN      : ")" ;

    PLUS        : "+" ;
    MINUS       : "-" ;
    TIMES       : "*" ;
    DIV         : "/" ;

    UINT        : r"[0-9]*" ;

}

grammar {

    companion {
        synthesis {
            value : Int ;
        }
        inheritance {
            left : Int ;
        }
        start : expression ;
    }

    expression      : term expressionPlus{ left = @0.value } :: { value = @1.value } ;
    expressionPlus  : PLUS  term expressionPlus{ left = left + @1.value } :: { value = @2.value }
                    | MINUS term expressionPlus{ left = left - @1.value } :: { value = @2.value }
                    | EPSILON :: { value = left } ;

    term            : factor termPlus{ left = @0.value } :: { value = @1.value } ;
    termPlus        : TIMES factor termPlus{ left = left * @1.value } :: { value = @2.value }
                    | DIV   factor termPlus{ left = left / @1.value } :: { value = @2.value }
                    | EPSILON :: { value = left } ;

    factor          : MINUS factor :: { value = -@1.value }
                    | atom :: { value = @0.value } ;
    atom            : LPAREN expression RPAREN :: { value = @1.value }
                    | UINT :: { value = @macro.toInt(@0.text) } ;

}