macro {

    fun toInt(value: String): Int {
        return value.toInt()
    }

    fun factorial(value: Int, cnt: Int): Int {
        fun fact(n: Int): Int {
            return if (n == 0) 1 else n * fact(n - 1)
        }
        return if (cnt == 0) value else factorial(fact(value), cnt - 1)
    }

}

tokens {

    companion {
        skip    : [WHITESPACE] ;
    }

    WHITESPACE  : r"[ \r\n\t]" ;

    LPAREN      : "(" ;
    RPAREN      : ")" ;

    PLUS        : "+" ;
    MINUS       : "-" ;
    TIMES       : "*" ;
    DIV         : "/" ;
    FACT        : "!" ;

    UINT        : r"[0-9]*" ;

}

grammar {

    companion {
        synthesis {
            value : Int ;
        }
        inheritance {
            left : Int ;
        }
        start : expression ;
    }

    expression      : term expressionPlus{ left = @0.value } :: { value = @1.value } ;
    expressionPlus  : PLUS  term expressionPlus{ left = @.left + @1.value } :: { value = @2.value }
                    | MINUS term expressionPlus{ left = @.left - @1.value } :: { value = @2.value }
                    | EPSILON :: { value = @.left } ;

    term            : factor termPlus{ left = @0.value } :: { value = @1.value } ;
    termPlus        : TIMES factor termPlus{ left = @.left * @1.value } :: { value = @2.value }
                    | DIV   factor termPlus{ left = @.left / @1.value } :: { value = @2.value }
                    | EPSILON :: { value = @.left } ;

    factor          : MINUS factor :: { value = -@1.value }
                    | primitive :: { value = @0.value } ;

    primitive       : atom factorial :: { value = @macro.factorial(@0.value, @1.value) };
    atom            : LPAREN expression RPAREN :: { value = @1.value }
                    | UINT :: { value = @macro.toInt(@0.text) } ;
    factorial       : FACT factorial :: { value = @1.value + 1 }
                    | EPSILON :: { value = 0 };

}