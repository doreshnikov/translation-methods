macro {

    fun tab(depth : Int): String {
        return "\t".repeat(depth)
    }

}

tokens {

    companion {
        skip    : [WHITESPACE] ;
    }

    fragments {
        LETTER  : "[a-zA-Z]" ;
        DIGIT   : "[0-9]" ;
        FIRST   : "($LETTER|_)" ;
    }

    WHITESPACE  : r"[ \r\n\t]" ;

    IFW         : "if" ;
    FORW        : "for" ;
    PASS        : "pass" ;
    BOTH        : "both" ;
    PRINTW      : "print" ;

    PLUS        : "+" ;
    MINUS       : "-" ;
    TIMES       : "*" ;
    DIV         : "/" ;

    NOT         : "!" ;
    XOR         : "^" ;
    AND         : "&" ;
    OR          : "|" ;

    GE          : ">=" ;
    GT          : ">" ;
    LE          : "<=" ;
    LT          : "<" ;
    EQ          : "==" ;
    NE          : "!=" ;

    ASSIGN      : "=" ;

    VAR         : r"$FIRST($FIRST|$DIGIT)*" ;
    UINT        : r"$DIGIT*" ;

}

grammar {

    companion {
        synthesis {
            view : String :: { default = @0.view } ;
        }
        inheritance {
            depth : Int :: { default = @.depth } ;
        }
        compute {
            tab : String :: { default = @macro.tab(depth) } ;
        }
        start : main ;
    }

    main                    : code :: { view = "fun main() {\n${@0.view}}\n" } ;

    code                    : codeBlock{ depth = 1 } code :: { view = "${@0.view}${@1.view}" }
                            | EPSILON :: { view = "" } ;
    codeBlock               : statement
                            | ifBlock
                            | forBlock
                            | PASS :: { view = "\n" } ;

    statement :: { view = "${value.tab}${@0.view}" }
                            : print
                            | assignment ;
    print                   : PRINTW expression :: { view = "print(${@1.view})\n" } ;
    assignment              : ASSIGN VAR expression :: { view = "${@1} = ${@2.view}\n" } ;

    ifBlock                 : IFW logicalExpression innerBody{ depth = @.depth + 1 } innerBody{ depth = @.depth + 1 } :: { view = "${value.tab}if (${@0.view}) {\n${@2.view}${value.tab}} else {\n${@3.view}${value.tab}}" } ;
    forBlock                : FORW VAR arithmeticAtom arithmeticAtom innerBody{ depth = @.depth + 1 } :: { view = "${value.tab}for (${@1} in ${@2.view}..${@3.view}) {\n${@4.view}${value.tab}}" } ;
    innerBody               : codeBlock
                            | BOTH codeBlock innerBody :: { view = "${@1.view}${@2.view}" } ;

    expression              : logicalExpression
                            | arithmeticExpression ;

    logicalBinop :: { view = @0.text }
                            : AND
                            | OR
                            | XOR ;
    logicalUnop :: { view = @0.text }
                            : NOT ;
    logicalExpression       : logicalBinop logicalExpression logicalExpression :: { view = "(${@1.view} ${@0.view} ${@2.view})" }
                            | logicalUnop logicalExpression :: { view = "${@0.view}${@1.view}" }
                            | logicalAtom ;
    logicalAtom             : VAR :: { view = @0.text }
                            | comparison ;

    relation :: { view = @0.text }
                            : EQ
                            | NE
                            | GT
                            | GE
                            | LT
                            | LE ;
    comparison              : relation arithmeticExpression arithmeticExpression :: { view = "(${@1.view} ${@0.view} ${@2.view})" } ;

    arithmeticBinop :: { view = @0.text }
                            : PLUS
                            | MINUS
                            | TIMES
                            | DIV ;
    arithmeticExpression    : arithmeticBinop arithmeticExpression arithmeticExpression :: { view ="(${@1.view} ${@0.view} ${@2.view})" }
                            | arithmeticAtom ;
    arithmeticAtom :: { view = @0.text }
                            : VAR
                            | UINT ;

}